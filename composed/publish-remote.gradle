apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}
artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll setup later
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = "The calculator Android library"
                    url = 'https://github.com/karthyks/Composed/tree/main'
                    licenses {
                        license {
                            name = 'MIT license'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'karthyks.dev'
                            name = 'karthyks'
                            email = 'mail2karthyk@gmail.com'
                        }
                    }

                    // Version control info
                    scm {
                        connection = 'scm:git:github.com/karthyks/Composed.git'
                        developerConnection = 'scm:git:ssh://github.com/karthyks/Composed.git'
                        url = 'https://github.com/karthyks/Composed/tree/main'
                    }
                }
            }
        }
        repositories {
            maven {
                name = "GitHubPackages"

                // Replace GITHUB_USERID with your personal or organisation user ID and
                // REPOSITORY with the name of the repository on GitHub
                url = uri("https://maven.pkg.github.com/karthyks/Composed")

                credentials {
                    username = rootProject.ext["signing.gitUser"]
                    password = rootProject.ext["signing.gitToken"]
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"]
    )
    sign publishing.publications
}